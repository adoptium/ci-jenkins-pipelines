plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group 'adoptopenjdk'
version '1.0.0-SNAPSHOT'

apply plugin: 'groovy'

// Get compile errors if using 11
sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        groovy {
            srcDirs = ['build', 'src/main/groovy', 'jenkins-helper/src/common']
            buildDir = 'target'
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/releases'
    }
    maven {
        url 'https://repo.jenkins-ci.org/public'
    }
}

shadowJar {
    archiveName = 'adopt-shared-lib.jar'
}

dependencies {
    compile group: 'org.jenkins-ci.main', name: 'jenkins-core', version: '2.85', ext: 'pom'
    compile group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-step-api', version: '2.22', ext: 'jar'
    compile 'org.codehaus.groovy:groovy-all:3.0.4'
    testCompile('org.junit.jupiter:junit-jupiter-api:5.4.1')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.4.1')
}

// Checkout Jenkins Helper so that it can be added to the classpath
task checkoutJenkinsHelper {
    doFirst {
        delete 'jenkins-helper'
        exec {
            commandLine 'git', 'clone', 'https://github.com/adoptium/jenkins-helper.git', 'jenkins-helper'
        }
    }
}

test {
    dependsOn checkoutJenkinsHelper
    useJUnitPlatform()
}
